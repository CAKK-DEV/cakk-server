plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.4'
}

allprojects {
	group = 'com.cakk'
	version = '1.0.0-SNAPSHOT'
	sourceCompatibility = '21'

	repositories {
		mavenCentral()
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'checkstyle'
	apply plugin: 'jacoco'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		// lombok
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		testImplementation 'junit:junit:4.13.1'
	}

	checkstyle {
		maxWarnings = 0
		configFile = file("$rootDir/config/checkstyle/checkstyle-rules.xml")
		configProperties = ["suppressionFile": "$rootDir/config/checkstyle/checkstyle-suppressions.xml"]
		sourceSets = [sourceSets.main]

		checkstyleMain {
			source = fileTree("src/main/java")
		}

		checkstyleTest {
			source = fileTree("src/test/java")
		}
	}

	tasks.named('test') {
		useJUnitPlatform()
		finalizedBy 'jacocoTestReport'
	}

	jacoco {
		toolVersion = "0.8.12"
	}

	jacocoTestReport {
		reports {
			html.required.set(true)
			csv.required.set(false)
			xml.required.set(true)
		}

		finalizedBy 'jacocoTestCoverageVerification'
	}

	jacocoTestCoverageVerification {
		def Qdomains = []
		for (qPattern in '*.QA'..'*.QZ') { // qPattern = '*.QA', '*.QB', ... '*.QZ'
			Qdomains.add(qPattern + '*')
		}

		violationRules {
			rule {
				enabled = true
				element = 'CLASS'

				limit {
					counter = 'LINE'
					value = 'COVEREDRATIO'
					minimum = 0.70
				}

				limit {
					counter = 'BRANCH'
					value = 'COVEREDRATIO'
					minimum = 0.70
				}

				limit {
					counter = "LINE"
					value = "TOTALCOUNT"
					maximum = "200".toBigDecimal()
				}

				excludes = [
				    "com.cakk.api.dto.**",
					"com.cakk.api.mapper.**",
					"com.cakk.api.vo.**",
					"com.cakk.domain.**"
				] + Qdomains as List<String>
			}
		}
	}
}

bootJar {
	enabled = false
}
jar {
	enabled = true
}
